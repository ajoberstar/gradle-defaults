plugins {
  id 'org.ajoberstar.defaults' version '0.11.0'
  id 'groovy'
  id 'java-gradle-plugin'
  // TODO remove when Gradle dependency locking is available
  id 'nebula.dependency-lock' version '5.0.4'
}

group = 'org.ajoberstar'

repositories {
  maven {
    name = 'Plugin Portal'
    url = 'https://plugins.gradle.org/m2/'
  }
  jcenter()
}

dependencies {
  // gradle
  compile gradleApi()
  compile localGroovy()

  // my plugins
  runtime 'org.ajoberstar:grgit:+'
  runtime 'org.ajoberstar.reckon:reckon-gradle:+'
  runtime 'org.ajoberstar:gradle-git-publish:+'
  runtime 'org.ajoberstar:gradle-stutter:+'

  // 3rd party plugins
  runtime 'com.gradle.publish:plugin-publish-plugin:+'
  runtime 'com.diffplug.spotless:spotless-plugin-gradle:+'
  runtime 'com.netflix.nebula:nebula-publishing-plugin:+'
}

publishing {
  repositories {
    maven {
      name = 'bintray'
      url = 'https://api.bintray.com/maven/ajoberstar/maven/gradle-defaults/;publish=1'
      credentials {
        username = System.env['BINTRAY_USER']
        password = System.env['BINTRAY_KEY']
      }
    }
  }
}

stutter {
  java(8) {
    compatibleRange '4.6'
  }
}

pluginBundle {
  website = 'https://github.com/ajoberstar/gradle-defaults'
  vcsUrl = 'https://github.com/ajoberstar/gradle-defaults.git'
  description = 'Plugin providing opinionated defaults for Gradle projects'
  plugins {
    publishPlugin {
      id = 'org.ajoberstar.defaults'
      displayName = 'ajoberstar Defaults Plugin'
      tags = ['defaults']
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}
