plugins {
  id 'org.ajoberstar.defaults' version '0.15.1'
  id 'com.gradle.plugin-publish' version '0.11.0'
  id 'groovy'
  id 'java-gradle-plugin'
}

apply from: 'gradle/locking.gradle'

group = 'org.ajoberstar'

repositories {
  maven {
    name = 'Plugin Portal'
    url = 'https://plugins.gradle.org/m2/'
  }
  jcenter()
}

dependencies {
  // gradle
  implementation gradleApi()
  implementation localGroovy()

  // my plugins
  implementation 'org.ajoberstar.grgit:grgit-gradle:latest.release'
  implementation 'org.ajoberstar.reckon:reckon-gradle:latest.release'
  implementation 'org.ajoberstar:gradle-git-publish:latest.release'
  implementation 'org.ajoberstar:gradle-stutter:latest.release'

  // 3rd party plugins
  implementation 'com.gradle.publish:plugin-publish-plugin:latest.release'
  implementation 'com.diffplug.spotless:spotless-plugin-gradle:latest.release'
}

publishing {
  repositories {
    maven {
      name = 'bintray'
      url = 'https://api.bintray.com/maven/ajoberstar/maven/gradle-defaults/;publish=1'
      credentials {
        username = System.env['BINTRAY_USER']
        password = System.env['BINTRAY_KEY']
      }
    }
  }
}

stutter {
  sparse=true

  java(8) {
    compatibleRange '4.8'
  }
}

pluginBundle {
  website = 'https://github.com/ajoberstar/gradle-defaults'
  vcsUrl = 'https://github.com/ajoberstar/gradle-defaults.git'
  description = 'Plugin providing opinionated defaults for Gradle projects'
  plugins {
    publishPlugin {
      id = 'org.ajoberstar.defaults'
      displayName = 'ajoberstar Defaults Plugin'
      tags = ['defaults']
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}
