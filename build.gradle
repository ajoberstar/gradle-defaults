buildscript {
  dependencies {
    classpath('org.eclipse.jgit:org.eclipse.jgit') {
      version { strictly '4.11.0.201803080745-r' }
      because '5.0 has a breaking change that needs to be fixed in grgit'
    }
    components.all {
      allVariants {
        withDependencies { deps ->
          deps.each { dep ->
            if (dep.group == 'org.eclipse.jgit') {
              dep.version {
                prefer '4.11.0.201803080745-r'
              }
              dep.because '5.0 has a breaking change that needs to be fixed in grgit'
            }
          }
        }
      }
    }
  }
}

plugins {
  id 'org.ajoberstar.defaults' version '0.12.0-rc.1'
  id 'groovy'
  id 'java-gradle-plugin'
  // TODO remove when Gradle dependency locking is available
  id 'nebula.dependency-lock' version '5.0.4'
}

group = 'org.ajoberstar'

repositories {
  maven {
    name = 'Plugin Portal'
    url = 'https://plugins.gradle.org/m2/'
  }
  jcenter()
}

dependencies {
  // gradle
  implementation gradleApi()
  implementation localGroovy()

  // my plugins
  implementation 'org.ajoberstar:grgit:latest.release'
  implementation 'org.ajoberstar.reckon:reckon-gradle:latest.release'
  implementation 'org.ajoberstar:gradle-git-publish:latest.release'
  implementation 'org.ajoberstar:gradle-stutter:latest.release'

  // 3rd party plugins
  implementation 'com.gradle.publish:plugin-publish-plugin:latest.release'
  implementation 'com.diffplug.spotless:spotless-plugin-gradle:latest.release'
  implementation 'com.netflix.nebula:nebula-publishing-plugin:latest.release'
}

publishing {
  repositories {
    maven {
      name = 'bintray'
      url = 'https://api.bintray.com/maven/ajoberstar/maven/gradle-defaults/;publish=1'
      credentials {
        username = System.env['BINTRAY_USER']
        password = System.env['BINTRAY_KEY']
      }
    }
  }
}

stutter {
  java(8) {
    compatibleRange '4.6'
  }
}

pluginBundle {
  website = 'https://github.com/ajoberstar/gradle-defaults'
  vcsUrl = 'https://github.com/ajoberstar/gradle-defaults.git'
  description = 'Plugin providing opinionated defaults for Gradle projects'
  plugins {
    publishPlugin {
      id = 'org.ajoberstar.defaults'
      displayName = 'ajoberstar Defaults Plugin'
      tags = ['defaults']
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}
